const std = @import("std");
const events = @import("events.zig");
const c = @cImport({
    @cInclude("SDL2/SDL.h");
    @cInclude("SDL2/SDL_ttf.h");
    @cInclude("SDL2/SDL_error.h");
    @cInclude("SDL2/SDL_render.h");
    @cInclude("SDL2/SDL_surface.h");
    @cInclude("SDL2/SDL_video.h");
});

var allocator: std.mem.Allocator = undefined;
const logger = std.log.scoped(.screen);
pub var pending: i32 = 0;
var missed: usize = 0;

const Gui = struct {
    window: *c.SDL_Window = undefined,
    render: *c.SDL_Renderer = undefined,
    width: u16 = 256,
    height: u16 = 128,
    zoom: u16 = 4,
    WIDTH: u16 = 256,
    HEIGHT: u16 = 128,
    ZOOM: u16 = 4,
    x: c_int = 0,
    y: c_int = 0,
};

pub const Texture = struct {
    texture: *c.SDL_Texture,
    width: u16,
    height: u16,
    zoom: u16 = 1,
};

pub const Vertex = struct {
    pub const Position = struct {
        x: f32 = 0,
        y: f32 = 0,
    };
    pub const Color = struct {
        r: u8 = 0,
        g: u8 = 0,
        b: u8 = 0,
        a: u8 = 0,
    };
    position: Position = .{},
    color: Color = .{},
    tex_coord: Position = .{},
};

var textures: std.ArrayList(Texture) = undefined;

var windows: [2]Gui = undefined;
var current: usize = 0;

var font: *c.TTF_Font = undefined;
var thread: std.Thread = undefined;
var quit = false;

pub fn define_geometry(texture: ?*const Texture, vertices: []const Vertex, indices: ?[]const usize) !void {
    var verts = try allocator.alloc(c.SDL_Vertex, vertices.len);
    defer allocator.free(verts);
    for (vertices, 0..) |v, i| {
        verts[i] = .{
            .position = .{
                .x = v.position.x,
                .y = v.position.y,
            },
            .color = .{
                .r = v.color.r,
                .g = v.color.g,
                .b = v.color.b,
                .a = v.color.a,
            },
            .tex_coord = .{
                .x = v.tex_coord.x,
                .y = v.tex_coord.y,
            },
        };
    }
    const txt = if (texture) |t| t.texture else null;
    const ind = if (indices) |i| blk: {
        var list = try allocator.alloc(c_int, i.len);
        for (list, 0..) |*l, j| {
            l.* = @intCast(i[j]);
        }
        break :blk list;
    } else null;
    defer if (ind) |i| allocator.free(i);
    const len = if (indices) |i| i.len else 0;
    sdl_call(c.SDL_RenderGeometry(
        windows[current].render,
        txt,
        verts.ptr,
        @intCast(verts.len),
        if (ind) |i| i.ptr else null,
        @intCast(len),
    ), "screen.define_geometry()");
}

pub fn triangle(ax: f32, ay: f32, bx: f32, by: f32, cx: f32, cy: f32) !void {
    var r: u8 = undefined;
    var g: u8 = undefined;
    var b: u8 = undefined;
    var a: u8 = undefined;
    const gui = windows[current];
    _ = c.SDL_GetRenderDrawColor(gui.render, &r, &g, &b, &a);
    const col = .{ .r = r, .g = g, .b = b, .a = a };
    const vertices = [3]Vertex{
        .{ .position = .{
            .x = ax,
            .y = ay,
        }, .color = col, .tex_coord = .{} },
        .{ .position = .{
            .x = bx,
            .y = by,
        }, .color = col, .tex_coord = .{} },
        .{ .position = .{
            .x = cx,
            .y = cy,
        }, .color = col, .tex_coord = .{} },
    };
    try define_geometry(null, &vertices, null);
}

pub fn quad(ax: f32, ay: f32, bx: f32, by: f32, cx: f32, cy: f32, dx: f32, dy: f32) !void {
    var r: u8 = undefined;
    var g: u8 = undefined;
    var b: u8 = undefined;
    var a: u8 = undefined;
    const gui = windows[current];
    _ = c.SDL_GetRenderDrawColor(gui.render, &r, &g, &b, &a);
    const col = .{ .r = r, .g = g, .b = b, .a = a };
    const vertices = [4]Vertex{
        .{ .position = .{
            .x = ax,
            .y = ay,
        }, .color = col, .tex_coord = .{} },
        .{ .position = .{
            .x = bx,
            .y = by,
        }, .color = col, .tex_coord = .{} },
        .{ .position = .{
            .x = cx,
            .y = cy,
        }, .color = col, .tex_coord = .{} },
        .{ .position = .{
            .x = dx,
            .y = dy,
        }, .color = col, .tex_coord = .{} },
    };
    try define_geometry(null, &vertices, null);
}

pub fn new_texture(width: u16, height: u16) !*Texture {
    const n: usize = @as(usize, width * windows[current].zoom) * @as(usize, height * windows[current].zoom) * 4;
    var pixels = try allocator.alloc(u8, n);
    defer allocator.free(pixels);
    sdl_call(c.SDL_RenderReadPixels(
        windows[current].render,
        &c.SDL_Rect{
            .x = windows[current].x,
            .y = windows[current].y,
            .w = width * windows[current].zoom,
            .h = height * windows[current].zoom,
        },
        c.SDL_PIXELFORMAT_RGBA32,
        pixels.ptr,
        width * windows[current].zoom * 4,
    ), "screen.new_texture()");
    const t = c.SDL_CreateTexture(
        windows[current].render,
        c.SDL_PIXELFORMAT_RGBA32,
        c.SDL_TEXTUREACCESS_STATIC,
        width * windows[current].zoom,
        height * windows[current].zoom,
    ) orelse {
        logger.err("{s}: error: {s}", .{ "screen.new_texture()", c.SDL_GetError() });
        return error.Fail;
    };
    sdl_call(c.SDL_UpdateTexture(
        t,
        null,
        pixels.ptr,
        width * windows[current].zoom * 4,
    ), "screen.new_texture()");
    sdl_call(c.SDL_LockTexture(t, null, null, null), "screen.new_texture()");
    var texture = textures.addOne() catch @panic("OOM!");
    texture.* = .{
        .texture = t,
        .width = width,
        .height = height,
        .zoom = windows[current].zoom,
    };
    return texture;
}

pub fn render_texture(texture: *const Texture, x: u16, y: u16) void {
    sdl_call(c.SDL_SetTextureBlendMode(
        texture.*.texture,
        c.SDL_BLENDMODE_BLEND,
    ), "screen.render_texture()");
    sdl_call(c.SDL_RenderCopy(
        windows[current].render,
        texture.*.texture,
        null,
        &c.SDL_Rect{ .x = x, .y = y, .w = texture.width, .h = texture.height },
    ), "screen.render_texture()");
}

pub fn render_texture_extended(
    texture: *const Texture,
    x: u16,
    y: u16,
    deg: f64,
    flip_h: bool,
    flip_v: bool,
) void {
    var flip = if (flip_h) c.SDL_FLIP_HORIZONTAL else 0;
    flip = flip | if (flip_v) c.SDL_FLIP_VERTICAL else 0;
    sdl_call(c.SDL_SetTextureBlendMode(
        texture.*.texture,
        c.SDL_BLENDMODE_BLEND,
    ), "screen.render_texture()");
    sdl_call(c.SDL_RenderCopyEx(
        windows[current].render,
        texture.*.texture,
        null,
        &c.SDL_Rect{ .x = x, .y = y, .w = texture.width, .h = texture.height },
        @floatCast(deg),
        null,
        @intCast(flip),
    ), "screen.render_texture()");
}

pub fn show(target: usize) void {
    c.SDL_ShowWindow(windows[target].window);
}

pub fn set(new: usize) void {
    current = new;
}

pub fn move(x: c_int, y: c_int) void {
    windows[current].x = x;
    windows[current].y = y;
}

pub fn move_rel(x: c_int, y: c_int) void {
    var gui = &windows[current];
    gui.x += x;
    gui.y += y;
}

pub fn refresh() void {
    c.SDL_RenderPresent(windows[current].render);
}

pub fn clear() void {
    sdl_call(
        c.SDL_SetRenderDrawColor(windows[current].render, 0, 0, 0, 255),
        "screen.clear()",
    );
    sdl_call(
        c.SDL_RenderClear(windows[current].render),
        "screen.clear()",
    );
}

pub fn color(r: u8, g: u8, b: u8, a: u8) void {
    sdl_call(
        c.SDL_SetRenderDrawColor(windows[current].render, r, g, b, a),
        "screen.color()",
    );
}

pub fn pixel(x: c_int, y: c_int) void {
    sdl_call(
        c.SDL_RenderDrawPoint(windows[current].render, x, y),
        "screen.pixel()",
    );
}

pub fn pixel_rel() void {
    const gui = windows[current];
    sdl_call(
        c.SDL_RenderDrawPoint(gui.render, gui.x, gui.y),
        "screen.pixel_rel()",
    );
}

pub fn line(bx: c_int, by: c_int) void {
    const gui = windows[current];
    sdl_call(
        c.SDL_RenderDrawLine(gui.render, gui.x, gui.y, bx, by),
        "screen.line()",
    );
}

pub fn line_rel(bx: c_int, by: c_int) void {
    const gui = windows[current];
    sdl_call(
        c.SDL_RenderDrawLine(gui.render, gui.x, gui.y, gui.x + bx, gui.y + by),
        "screen.line()",
    );
}

pub fn rect(w: i32, h: i32) void {
    const gui = windows[current];
    var r = c.SDL_Rect{ .x = gui.x, .y = gui.y, .w = w, .h = h };
    sdl_call(
        c.SDL_RenderDrawRect(gui.render, &r),
        "screen.rect()",
    );
}

pub fn rect_fill(w: i32, h: i32) void {
    const gui = windows[current];
    var r = c.SDL_Rect{ .x = gui.x, .y = gui.y, .w = w, .h = h };
    sdl_call(
        c.SDL_RenderFillRect(gui.render, &r),
        "screen.rect_fill()",
    );
}

pub fn text(words: [:0]const u8) void {
    if (words.len == 0) return;
    var r: u8 = undefined;
    var g: u8 = undefined;
    var b: u8 = undefined;
    var a: u8 = undefined;
    const gui = windows[current];
    _ = c.SDL_GetRenderDrawColor(gui.render, &r, &g, &b, &a);
    var col = c.SDL_Color{ .r = r, .g = g, .b = b, .a = a };
    var text_surf = c.TTF_RenderText_Solid(font, words, col);
    var texture = c.SDL_CreateTextureFromSurface(gui.render, text_surf);
    const rectangle = c.SDL_Rect{ .x = gui.x, .y = gui.y, .w = text_surf.*.w, .h = text_surf.*.h };
    sdl_call(
        c.SDL_RenderCopy(gui.render, texture, null, &rectangle),
        "screen.text()",
    );
    c.SDL_DestroyTexture(texture);
    c.SDL_FreeSurface(text_surf);
}

pub fn text_center(words: [:0]const u8) void {
    if (words.len == 0) return;
    var r: u8 = undefined;
    var g: u8 = undefined;
    var b: u8 = undefined;
    var a: u8 = undefined;
    const gui = windows[current];
    _ = c.SDL_GetRenderDrawColor(gui.render, &r, &g, &b, &a);
    var col = c.SDL_Color{ .r = r, .g = g, .b = b, .a = a };
    var text_surf = c.TTF_RenderText_Solid(font, words, col);
    var texture = c.SDL_CreateTextureFromSurface(gui.render, text_surf);
    const radius = @divTrunc(text_surf.*.w, 2);
    const rectangle = c.SDL_Rect{ .x = gui.x - radius, .y = gui.y, .w = text_surf.*.w, .h = text_surf.*.h };
    sdl_call(
        c.SDL_RenderCopy(gui.render, texture, null, &rectangle),
        "screen.text()",
    );
    c.SDL_DestroyTexture(texture);
    c.SDL_FreeSurface(text_surf);
}

pub fn text_right(words: [:0]const u8) void {
    if (words.len == 0) return;
    var r: u8 = undefined;
    var g: u8 = undefined;
    var b: u8 = undefined;
    var a: u8 = undefined;
    const gui = windows[current];
    _ = c.SDL_GetRenderDrawColor(gui.render, &r, &g, &b, &a);
    var col = c.SDL_Color{ .r = r, .g = g, .b = b, .a = a };
    var text_surf = c.TTF_RenderText_Solid(font, words, col);
    var texture = c.SDL_CreateTextureFromSurface(gui.render, text_surf);
    const width = text_surf.*.w;
    const rectangle = c.SDL_Rect{ .x = gui.x - width, .y = gui.y, .w = width, .h = text_surf.*.h };
    sdl_call(
        c.SDL_RenderCopy(gui.render, texture, null, &rectangle),
        "screen.text()",
    );
    c.SDL_DestroyTexture(texture);
    c.SDL_FreeSurface(text_surf);
}

pub fn arc(radius: i32, theta_1: f64, theta_2: f64) void {
    std.debug.assert(0 <= theta_1);
    std.debug.assert(theta_1 <= theta_2);
    std.debug.assert(theta_2 <= std.math.tau);
    const angle_length = (theta_2 - theta_1) * @as(f64, @floatFromInt(radius));
    const perimeter_estimate: usize = 2 * @as(usize, @intFromFloat(angle_length)) + 9;
    const gui = windows[current];
    var points = std.ArrayList(c.SDL_Point).initCapacity(allocator, perimeter_estimate) catch @panic("OOM!");
    defer points.deinit();
    var offset_x: i32 = 0;
    var offset_y: i32 = radius;
    var d = radius - 1;
    while (offset_y >= offset_x) {
        const pts = [8]c.SDL_Point{ .{
            .x = gui.x + offset_x,
            .y = gui.y + offset_y,
        }, .{
            .x = gui.x + offset_y,
            .y = gui.y + offset_x,
        }, .{
            .x = gui.x - offset_x,
            .y = gui.y + offset_y,
        }, .{
            .x = gui.x - offset_y,
            .y = gui.y + offset_x,
        }, .{
            .x = gui.x + offset_x,
            .y = gui.y - offset_y,
        }, .{
            .x = gui.x + offset_y,
            .y = gui.y - offset_x,
        }, .{
            .x = gui.x - offset_x,
            .y = gui.y - offset_y,
        }, .{
            .x = gui.x - offset_y,
            .y = gui.y - offset_x,
        } };
        for (pts) |pt| {
            const num: f64 = @floatFromInt(pt.x);
            const denom: f64 = @floatFromInt(pt.y);
            const theta = std.math.atan(num / denom);
            if (theta_1 <= theta and theta <= theta_2) {
                points.appendAssumeCapacity(pt);
            }
        }
        if (d >= 2 * offset_x) {
            d -= 2 * offset_x + 1;
            offset_x += 1;
        } else if (d < 2 * (radius - offset_y)) {
            d += 2 * offset_y - 1;
            offset_y -= 1;
        } else {
            d += 2 * (offset_y - offset_x - 1);
            offset_y -= 1;
            offset_x += 1;
        }
    }
    const slice = points.items;
    sdl_call(
        c.SDL_RenderDrawPoints(gui.render, slice.ptr, @intCast(slice.len)),
        "screen.arc()",
    );
}

pub fn circle(radius: i32) void {
    const perimeter_estimate: usize = @intFromFloat(2 * std.math.tau * @as(f64, @floatFromInt(radius)));
    const gui = windows[current];
    var points = std.ArrayList(c.SDL_Point).initCapacity(allocator, perimeter_estimate) catch @panic("OOM!");
    defer points.deinit();
    var offset_x: i32 = 0;
    var offset_y: i32 = radius;
    var d = radius - 1;
    while (offset_y >= offset_x) {
        const pts = [8]c.SDL_Point{ .{
            .x = gui.x + offset_x,
            .y = gui.y + offset_y,
        }, .{
            .x = gui.x + offset_y,
            .y = gui.y + offset_x,
        }, .{
            .x = gui.x - offset_x,
            .y = gui.y + offset_y,
        }, .{
            .x = gui.x - offset_y,
            .y = gui.y + offset_x,
        }, .{
            .x = gui.x + offset_x,
            .y = gui.y - offset_y,
        }, .{
            .x = gui.x + offset_y,
            .y = gui.y - offset_x,
        }, .{
            .x = gui.x - offset_x,
            .y = gui.y - offset_y,
        }, .{
            .x = gui.x - offset_y,
            .y = gui.y - offset_x,
        } };
        points.appendSliceAssumeCapacity(&pts);
        if (d >= 2 * offset_x) {
            d -= 2 * offset_x + 1;
            offset_x += 1;
        } else if (d < 2 * (radius - offset_y)) {
            d += 2 * offset_y - 1;
            offset_y -= 1;
        } else {
            d += 2 * (offset_y - offset_x - 1);
            offset_y -= 1;
            offset_x += 1;
        }
    }
    const slice = points.items;
    sdl_call(
        c.SDL_RenderDrawPoints(gui.render, slice.ptr, @intCast(slice.len)),
        "screen.circle()",
    );
}

pub fn circle_fill(radius: i32) void {
    const r = if (radius < 0) -radius else radius;
    const rsquared = radius * radius;
    const gui = windows[current];
    var points = std.ArrayList(c.SDL_Point).initCapacity(allocator, @intCast(4 * rsquared + 2)) catch @panic("OOM!");
    defer points.deinit();
    var i = -r;
    while (i <= r) : (i += 1) {
        var j = -r;
        while (j <= r) : (j += 1) {
            if (i * i + j * j < rsquared) points.appendAssumeCapacity(.{
                .x = gui.x + i,
                .y = gui.y + j,
            });
        }
    }
    const slice = points.items;
    sdl_call(
        c.SDL_RenderDrawPoints(gui.render, slice.ptr, @intCast(slice.len)),
        "screen.circle_fill()",
    );
}

const Size = struct {
    w: i32,
    h: i32,
};

pub fn get_text_size(str: [*:0]const u8) Size {
    var w: i32 = undefined;
    var h: i32 = undefined;
    sdl_call(c.TTF_SizeText(font, str, &w, &h), "screen.get_text_size()");
    return .{ .w = w, .h = h };
}

pub fn get_size() Size {
    return .{
        .w = windows[current].width,
        .h = windows[current].height,
    };
}

pub fn set_size(width: i32, height: i32, zoom: i32) void {
    const gui = &windows[current];
    gui.WIDTH = @intCast(width);
    gui.HEIGHT = @intCast(height);
    gui.ZOOM = @intCast(zoom);
    c.SDL_SetWindowSize(gui.window, width * zoom, height * zoom);
    c.SDL_SetWindowMinimumSize(gui.window, width, height);
    window_rect(gui);
}

pub fn set_fullscreen(is_fullscreen: bool) void {
    const gui = windows[current];
    if (is_fullscreen) {
        sdl_call(
            c.SDL_SetWindowFullscreen(gui.window, c.SDL_WINDOW_FULLSCREEN_DESKTOP),
            "screen.set_fullscreen()",
        );
        window_rect(&windows[current]);
    } else {
        sdl_call(
            c.SDL_SetWindowFullscreen(gui.window, 0),
            "screen.set_fullscreen()",
        );
        set_size(gui.WIDTH, gui.HEIGHT, gui.ZOOM);
    }
    const event = .{
        .Screen_Resized = .{
            .w = gui.width,
            .h = gui.height,
            .window = current,
        },
    };
    events.post(event);
}

pub fn init(alloc_pointer: std.mem.Allocator, width: u16, height: u16, resources: []const u8) !void {
    allocator = alloc_pointer;

    if (c.SDL_Init(c.SDL_INIT_VIDEO) < 0) {
        logger.err("screen.init(): {s}", .{c.SDL_GetError()});
        return error.Fail;
    }

    if (c.TTF_Init() < 0) {
        logger.err("screen.init(): {s}", .{c.TTF_GetError()});
        return error.Fail;
    }

    const filename = try std.fmt.allocPrintZ(allocator, "{s}/04b03.ttf", .{resources});
    defer allocator.free(filename);
    var f = c.TTF_OpenFont(filename, 8);
    font = f orelse {
        logger.err("screen.init(): {s}", .{c.TTF_GetError()});
        return error.Fail;
    };

    for (0..2) |i| {
        var w = c.SDL_CreateWindow(
            if (i == 0) "seamstress" else "seamstress_params",
            0,
            @intCast(i * height * 4),
            width * 4,
            height * 4,
            c.SDL_WINDOW_SHOWN | c.SDL_WINDOW_RESIZABLE,
        );
        var window = w orelse {
            logger.err("screen.init(): {s}", .{c.SDL_GetError()});
            return error.Fail;
        };
        var r = c.SDL_CreateRenderer(window, 0, 0);
        var render = r orelse {
            logger.err("screen.init(): {s}", .{c.SDL_GetError()});
            return error.Fail;
        };
        c.SDL_SetWindowMinimumSize(window, width, height);
        windows[i] = .{
            .window = window,
            .render = render,
            .zoom = 4,
            .WIDTH = width,
            .HEIGHT = height,
            .ZOOM = 4,
        };
        set(i);
        window_rect(&windows[current]);
        clear();
        refresh();
    }
    set(0);
    textures = std.ArrayList(Texture).init(allocator);
    thread = try std.Thread.spawn(.{}, loop, .{});
}

fn window_rect(gui: *Gui) void {
    var xsize: i32 = undefined;
    var ysize: i32 = undefined;
    var xzoom: u16 = 1;
    var yzoom: u16 = 1;
    const oldzoom = gui.zoom;
    c.SDL_GetWindowSize(gui.window, &xsize, &ysize);
    while ((1 + xzoom) * gui.WIDTH <= xsize) : (xzoom += 1) {}
    while ((1 + yzoom) * gui.HEIGHT <= ysize) : (yzoom += 1) {}
    gui.zoom = if (xzoom < yzoom) xzoom else yzoom;
    const uxsize: u16 = @intCast(xsize);
    const uysize: u16 = @intCast(ysize);
    gui.width = @divFloor(uxsize, gui.zoom);
    gui.height = @divFloor(uysize, gui.zoom);
    gui.x = @divFloor(gui.x * oldzoom, gui.zoom);
    gui.y = @divFloor(gui.y * oldzoom, gui.zoom);
    sdl_call(c.SDL_RenderSetScale(
        gui.render,
        @floatFromInt(gui.zoom),
        @floatFromInt(gui.zoom),
    ), "window_rect()");
}

pub fn check() void {
    var ev: c.SDL_Event = undefined;
    while (c.SDL_PollEvent(&ev) != 0) {
        switch (ev.type) {
            c.SDL_KEYUP, c.SDL_KEYDOWN => {
                const event = .{
                    .Screen_Key = .{
                        .sym = ev.key.keysym.sym,
                        .mod = ev.key.keysym.mod,
                        .repeat = ev.key.repeat > 0,
                        .state = ev.key.state == c.SDL_PRESSED,
                        .window = ev.key.windowID,
                    },
                };
                events.post(event);
            },
            c.SDL_QUIT => {
                events.post(.{ .Quit = {} });
                quit = true;
            },
            c.SDL_MOUSEMOTION => {
                const zoom: f64 = @floatFromInt(windows[ev.button.windowID - 1].zoom);
                const x: f64 = @floatFromInt(ev.button.x);
                const y: f64 = @floatFromInt(ev.button.y);
                const event = .{
                    .Screen_Mouse_Motion = .{
                        .x = x / zoom,
                        .y = y / zoom,
                        .window = ev.motion.windowID,
                    },
                };
                events.post(event);
            },
            c.SDL_MOUSEBUTTONDOWN, c.SDL_MOUSEBUTTONUP => {
                const zoom: f64 = @floatFromInt(windows[ev.button.windowID - 1].zoom);
                const x: f64 = @floatFromInt(ev.button.x);
                const y: f64 = @floatFromInt(ev.button.y);
                const event = .{
                    .Screen_Mouse_Click = .{
                        .state = ev.button.state == c.SDL_PRESSED,
                        .x = x / zoom,
                        .y = y / zoom,
                        .button = ev.button.button,
                        .window = ev.button.windowID,
                    },
                };
                events.post(event);
            },
            c.SDL_WINDOWEVENT => {
                switch (ev.window.event) {
                    c.SDL_WINDOWEVENT_CLOSE => {
                        if (ev.window.windowID == 1) {
                            events.post(.{ .Quit = {} });
                            quit = true;
                        } else {
                            c.SDL_HideWindow(windows[ev.window.windowID - 1].window);
                        }
                    },
                    c.SDL_WINDOWEVENT_EXPOSED => {
                        const old = current;
                        set(ev.window.windowID - 1);
                        refresh();
                        set(old);
                    },
                    c.SDL_WINDOWEVENT_RESIZED => {
                        const old = current;
                        const id = ev.window.windowID - 1;
                        set(id);
                        window_rect(&windows[current]);
                        refresh();
                        set(old);
                        const event = .{
                            .Screen_Resized = .{
                                .w = windows[id].width,
                                .h = windows[id].height,
                                .window = id + 1,
                            },
                        };
                        events.post(event);
                    },
                    else => {},
                }
            },
            else => {},
        }
    }
}

pub fn deinit() void {
    quit = true;
    thread.join();
    if (missed > 0) logger.warn("missed {d} events", .{missed});
    for (textures.items) |texture| {
        c.SDL_DestroyTexture(texture.texture);
    }
    textures.deinit();
    c.TTF_CloseFont(font);
    var i: usize = 0;
    while (i < 2) : (i += 1) {
        c.SDL_DestroyRenderer(windows[i].render);
        c.SDL_DestroyWindow(windows[i].window);
    }
    c.TTF_Quit();
    c.SDL_Quit();
}

fn loop() void {
    while (!quit) {
        if (pending < 100) {
            events.post(.{ .Screen_Check = {} });
            pending += 1;
        } else missed += 1;
        std.time.sleep(10 * std.time.ns_per_ms);
    }
}

fn sdl_call(err: c_int, name: []const u8) void {
    if (err < 0) {
        logger.err("{s}: error: {s}", .{ name, c.SDL_GetError() });
    }
}
